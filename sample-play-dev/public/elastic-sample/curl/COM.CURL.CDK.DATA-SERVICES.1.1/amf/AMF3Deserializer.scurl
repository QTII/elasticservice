||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

|| Deserialize AMF3
{define-class public open AMF3Deserializer {inherits AMFDeserializer}

  field private constant references:AMF3References
  field private constant mapper:AMFObjectAdapters

  {constructor public {default
                          input:ByteInputStream,
                          mapper:AMFObjectAdapters = AMFObjectAdapters.standard}
    set self.references = {AMF3References}
    set self.mapper = mapper
    {construct-super input}
  }

  {method public open {read-one}:any
    def type = {marshal-in-uint8 self.input}
    {return
        {switch type
         case AMF3Marker.undefined-marker.value do  null
         case AMF3Marker.null-marker.value do       null
         case AMF3Marker.false-marker.value do      false
         case AMF3Marker.true-marker.value do       true
         case AMF3Marker.integer-marker.value do    {self.read-integer}
         case AMF3Marker.double-marker.value do     {marshal-in-double self.input}
         case AMF3Marker.string-marker.value do     {self.read-string}
         case AMF3Marker.xml-doc-marker.value do    {self.read-xml-document}
         case AMF3Marker.date-marker.value do       {self.read-date}
         case AMF3Marker.array-marker.value do      {self.read-array}
         case AMF3Marker.object-marker.value do     {self.read-object}
||--         case AMF3Marker.xml-marker.value do        {self.read-xml}
         case AMF3Marker.byte-array-marker.value do {self.read-byte-array}
         else
            {throw {AMFException {lmessage Unsupported AMF3 type {value type}.}}}
        }
    }
  }

  || read amf3 integers. 29bit integer
  {method public {read-integer}:int
    let n:int = 0
    let result:int = 0

    {while true do
        let b:byte = {self.input.read-one}
        {if n == 3 then
            set result = {bit-or {bit-sll result, 8}, b}
            {if {bit-and result, 0x10000000} != 0 then
                set result = {bit-or result, 0xF0000000}
            }
            {break}
         else
            set result = {bit-or {bit-sll result, 7}, {bit-and b, 0x7f}}
            {if {bit-and b, 0x80} == 0 then
                {break}
            }
        }
        {inc n}
    }

    {return result}
  }

  || read amf3 strings.
  {method public {read-string}:String
    def type = {self.read-integer}
    def (inline?, n) = {self.check-reference type}
    
    {if inline? then
        def (num-bytes-decoded, chars) =
            {decode-characters
                {self.input.read n = n}.underlying-FastArray,
                CharEncoding.utf8
            }
        {if not chars.empty? then
            {self.references.add-string chars}
        }
        {return chars}
     else
        {return {self.references.get-string n}}
    }
  }

  || TODO: adapter for XDMDocument
  {method public open {read-xml-document}:Object
    def type = {self.read-integer}
    def (inline?, n) = {self.check-reference type}

    {if inline? then
        def (num-bytes-decoded, chars) =
            {decode-characters
                {self.input.read n = n}.underlying-FastArray,
                CharEncoding.utf8
            }
        def xml = {AMFXmlDocument chars}
        {self.references.add-object xml}
        
        {return xml}
     else
        {return {self.references.get-object n}}
    }
  }

  {method public open {read-date}:DateTime
    def type = {self.read-integer}
    def (inline?, n) = {self.check-reference type}

    {if inline? then
        let (seconds:double, partial-seconds:double) =
            {floor {marshal-in-double self.input}, 1000.0}
        let dt:DateTime =
            {DateTime
                {DateTimeData
                    DateTimeData.epoch-simple.epoch-second +
                    (seconds asa int64),
                    partial-second =
                        (partial-seconds / 1000.0 * {ldexp 1.0, 63}) asa int64
                },
                zone = DateTimeZone.utc
            }
        {self.references.add-object dt}
        {return dt}
     else
        {return {self.references.get-object n} asa DateTime}
    }
  }

  {method public open {read-xml}:Object
    || TODO: whats the difference? 
    {return {self.read-xml-document}}
  }

  {method private {read-array}:ExtendedArray
    def type = {self.read-integer}
    def (inline?, n) = {self.check-reference type}
    {if not inline? then
        {return {self.references.get-object n} asa ExtendedArray}
    }

    def array = {ExtendedArray efficient-size = n}
    {self.references.add-object array}

    || read the associative portion
    def ap = {DataObject}
    {while true do
        def name = {self.read-string}
        {if name.empty? then {break}}
        set ap[name] = {self.read-one}
    }
    {if not ap.empty? then
        set array.data = ap}

    || read the dense portion
    {for i:int=0 below n do
        {array.append {self.read-one}}
    }
    
    {return array}
  }

  {method public open {read-byte-array}:ByteArray
    def type = {self.read-integer}
    def (inline?, n) = {self.check-reference type}

    {if inline? then
        def ret = {self.input.read n = n}
        {self.references.add-object ret}
        {return ret}
     else
        {return {self.references.get-object n} asa ByteArray}
    }
  }

  {method public open {read-object}:Object
    def type = {self.read-integer}
    def (obj-inline?, obj-index) = {self.check-reference type}
    {if not obj-inline? then
        {return {self.references.get-object obj-index} asa Object}
    }

    def trait = {self.read-trait type}
    def class-name = trait.name
    
    {if trait.externalizable? then
        
        || externalized
        {return {self.read-externalizable class-name}}
     else

        || specialized adapter
        {if-non-null ret = {self.read-default-object trait} then
            {return ret}
        }

        || DataObject
        def obj = {DataObject.from-traits trait}
        {self.references.add-object obj}

        {for member key index in trait.members do
            {obj.set-internal index, {self.read-one}}
        }
        
        {if trait.dynamic? then
            {while true do
                def name = {self.read-string}
                {if name.empty? then {break}}
                set obj[name] = {self.read-one}
            }
            {obj.commit}
        }
        {return obj}
    }
  }

  {method protected open {read-trait type:int}:DataObjectTraits
    def ref? = ({bit-and type, 0x3} == 1)

    {if ref? then
        def trait = {self.references.get-trait {bit-srl type, 2}}
        {return trait}
     else
        def externalizable? = ({bit-and type, 0x4} != 0)
        def dynamic? = ({bit-and type, 0x8} != 0)
        def mem-count = {bit-srl type, 4}
        def class-name = {self.read-string}

        def members = {{FastArray-of String} max-size = mem-count}
        def _trait =
            {StandardDataObjectTraits class-name,
                {StringArray.from-FastArray members},
                externalizable? = externalizable?,
                dynamic? = dynamic?}
        
        {for i = 0 below mem-count do
            {members.append {self.read-string}}
        }
        {_trait.generate-index}
        def trait = {DataObjectTraits.intern _trait}
        
        {self.references.add-trait trait}
        
        {return trait}
    }
  }

  {method protected open {read-externalizable class-name:String}:Object
    def (class, class?) =
        {self.mapper.class-for class-name}
    {if class? and {class.subtype-of? Externalizable} then
        {with-compiler-directives allow-any-calls? = true,
            allow-implicit-any-casts? = true
         do
            def ext = {class}
            {self.references.add-object ext}
            {ext.read-externalizable self}
            {return ext}}
     else
        {throw {AMFException {lmessage No 'read-externalizable' handler for {value class-name}.}}}
    }
  }
  
  {method protected open {read-default-object traits:DataObjectTraits}:#Object
    || specialized factory registered for this type
    def (factory, factory?) =
        {self.mapper.factory-for traits.name}
    
    {if not factory? then
        {return null}
    }
    
    def obj = {factory}
    {self.references.add-object obj}

    {type-switch obj
        
     case obj:Externalizable do
        || use externalizer if possible
        {obj.read-externalizable self}

     else
        || by reflection
        || NOTE: this is SLOW
||--        def type = {type-of obj} asa ClassType
||--        {for member in traits.members do
||--            def pval = {self.read-one}
||--            {if-non-null property =
||--                {type.get-property-for-set {curlify-name member}}
||--             then
||--                {type-switch pval
||--                 case pval:Null do
||--                    || ignore
||--                 else
||--                    {property.set-value obj, pval}
||--                }
||--            }
||--        }
        
        || predefined in adapter
        def reader = {self.mapper.reader-for traits}
        {reader obj, self}
    }
    {return obj}
  }
  
  {method private final inline {check-reference type:int}:(flag?:bool, n:int)
    def flag? = ({bit-and type, 0x01} != 0)
    def n = {bit-srl type, 1}
    {return flag?, n}
  }
}
