||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{define-class public TileSelector {inherits DiscreteGraphicSelectionFrame}
  field public-get constant contents:RasterBox
  field public-get value:any
  field protected selection-background:#Background
  field protected rollover-background:#Background
  {nonlocal-option public rollover-color:#FillPattern
    {if-non-null rollover-color then
        set self.rollover-background = {Background rollover-color}
    }
  }
  {local-option public selection-color:#FillPattern = null
    {if-non-null selection-color then
        set self.selection-background = {Background selection-color}
    }
  }
  {constructor public {default ...}
    set self.contents = {RasterBox}
    def scrollbox =
        {ScrollBox
            hscroll? = false,
            self.contents
        }
    def scrollbar = {scrollbox.get-vscroll}
    set scrollbar.accent-color = "#e57000"
    {install-style-sheet-on-Graphic
        scrollbar,
        {self.get-scrollbar-style-sheet}
    }
    {construct-super
        _style-element = "TileSelector",
        scrollbox,
        multiple-selection-enabled? = false
    }
    {self.add-event-handler
        {on SelectionChanged do
            {self.handle-selection-changed}
        }
    }
    {self.process-rest-args {splice ...}}
  }
  {method {handle-selection-changed}:void
    def val =
        {type-switch self.selection
         case s:DiscreteSelection do
            {if s.empty? then
                null
             else
                {type-switch {s.items.read-one}.visual-or-null
                 case v:Visual do
                    v.user-data
                 else
                    null
                }
            }
         else
            null
        }
    set self.value = val
    {self.handle-event {ValueChanged}}
  }
  {method public {overdraw-selection
                     g:Graphic,
                     r:Renderer2d
                 }:void
    {if-non-null sel = g.graphic-selectable then
        {if {self.selection.contains? sel} then
            {if-non-null selection-color = self.selection-color then
                {if-non-null bg = self.selection-background then
                    {if g.background != self.rollover-background then
                        set g.background = bg
                    }
                }
             else
                {super.overdraw-selection g, r}
            }
         else
            {if-non-null selection-color = self.selection-color then
                {if g.background == self.selection-background then
                    {unset g.background}
                }
            }
        }
    }
  }

  {method protected {get-scrollbar-style-sheet}:StyleSheet
    {return
        {StyleSheet
            {StyleRule "Scrollbar",
                thumb-skin =
                    {ShadedSkinSpec 
                        "white",
                        "white",
                        border-color = "gray",
                        rollover-border-color = "#e57000",
                        gradient-angle = 0deg,
                        highlight-alpha-1 = 0.2,
                        highlight-alpha-2 = 0.1,
                        highlight-boundary = .05,
                        overlay-decoration = "grip",
                        corner-radius = 3px
                    },
                channel-skin = 
                    {ShadedSkinSpec 
                        "#9ba0a2",
                        "#e7e7e7",
                        border-color = "gray",
                        rollover-border-color = "gray",
                        gradient-angle = -90deg,
                        highlight-alpha-1 = 0.0,
                        highlight-alpha-2 = 0.0,
                        highlight-boundary = .35,
                        corner-radius = 0px,
                        orientation-transform = "rotate-base"
                    }
            },
            {StyleRule "Scrollbar/decrease-button",
                skin = {ShadedSkinSpec
                           "white",
                           "white",
                           border-color = "gray",
                           rollover-border-color = "#e57000",
                           overlay-decoration = "decrease-triangle",
                           highlight-boundary = .05
                       }
            },
            {StyleRule "Scrollbar/increase-button",
                skin = {ShadedSkinSpec
                           "white",
                           "white",
                           border-color = "gray",
                           rollover-border-color = "#e57000",
                           overlay-decoration = "increase-triangle",
                           highlight-boundary = .05
                       }
            }
        }
    }
  }
}
