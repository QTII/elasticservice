||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{define-class public Producer {inherits AbstractProducer}
  field protected _subtopic:String

  {doc-next
    {purpose
        Create a {docref Producer} with some server information.
    }
    {parameter endpoint,
        The {docref Url} of the server that this object is
        going to communicate with.  Also if no {docref channel-set} is
        provided, this will be used to make a {docref ChannelSet} wrapped
        around a {docref AMFChannel}.
    }
    {parameter destination,
        The destination at the {param endpoint} for all messages being sent.
    }
    {parameter channel-set,
        A {docref ChannelSet} to use to send messages, this will be
        implicitly created from the {param endpoint} if it is not supplied.
    }
    {parameter subtopic,
        The subtopic understood by the destination to send to.
    }
    {parameter ...,
        {docref EventHandler}s to receive events on this object.
    }
    {notes
        The {docref EventHandler}s on this object can receive
        {docref MessageFaultEvent}s, {docref MessageAckEvent}s,
        {docref ChannelEvent}s, and {docref ChannelFaultEvent}s.
    }
  }
  {constructor public {default
                          endpoint:#Url = null,
                          destination:String = "",
                          channel-set:#ChannelSet = null,
                          subtopic:String = "",
                          ...:EventHandler
                      }
    {construct-super
        endpoint = endpoint,
        destination = destination,
        channel-set = channel-set,
        ...
    }
    set self._subtopic = subtopic
  }

  {method protected {internal-send
                     message:IMessage,
                     wait-for-client-id?:bool = true
                 }:void
    {if not self._subtopic.empty? then
        set message.headers[AsyncMessage.SUBTOPIC_HEADER] = self._subtopic
    }

    {super.internal-send message, wait-for-client-id? = wait-for-client-id?}
  }

  {doc-next
    {purpose
        The subtopic understood by the destination to subscribe to.
    }
  }
  {getter public {subtopic}:String
    {return self._subtopic}
  }
  {setter public {subtopic val:String}
    {if self._subtopic != val then
        def event = {PropertyChangeEvent.create-update-event
                        self, "subtopic", self._subtopic, val
                    }
        set self._subtopic = val
        {self.enqueue-event event}
    }
  }
}
