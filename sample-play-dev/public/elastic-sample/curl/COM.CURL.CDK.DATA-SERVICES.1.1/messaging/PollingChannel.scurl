||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{define-class public PollingChannel {inherits Channel}
  field private _polling-enabled?:bool
  field protected should-poll?:bool
  field private _polling-interval:Time =
      PollingChannel.DEFAULT_POLLING_INTERVAL
  field private _piggybacking-enabled?:bool = false
  field protected poll-outstanding?:bool = false
  field protected polling-ref:int = -1
  field private _alarm:#Alarm

  let protected constant DEFAULT_POLLING_INTERVAL:Time = 3000ms

  {constructor public {default id:#String = null, uri:#Url = null}
    {construct-super id = id, uri = uri}
    set self._polling-enabled? = true
    set self.should-poll? = false
    {if {self.alarm-required?} then
        set self._alarm = {Alarm
                              {proc {}:void
                                  {self.internal-poll}
                              }
                          }
    }
  }

  {getter protected {polling-enabled?}:bool
    {return self._polling-enabled?}
  }

  {setter protected {polling-enabled? b?:bool}:void
    set self._polling-enabled? = b?
    {if not b? and
        ({self.alarm-running?} or
         (not {self.alarm-running?} and self._polling-interval == 0s))
     then
        {self.stop-polling}

     elseif b? and self.should-poll? and not {self.alarm-running?} then
        {self.start-polling}
    }
  }

  {getter protected {polling-interval}:Time
    {if self._alarm != null then
        {return self._polling-interval}
     else
        {return 0s}
    }
  }

  {setter protected {polling-interval t:Time}:void
    {if t == 0s then
        set self._polling-interval = t
        {if-non-null alarm = self._alarm then
            {alarm.cancel}
        }
        {if self.should-poll? then
            {self.start-polling}
        }
     elseif t > 0s then
        {if-non-null alarm = self._alarm then
            set self._polling-interval = t
            {if not alarm.scheduled? and self.should-poll? then
                {self.start-polling}
            }
        }
     else
        {error {lmessage Negative interval not allowed.}}
    }
  }

  {getter protected {piggybacking-enabled?}:bool
    {return self._piggybacking-enabled?}
  }

  {setter protected {piggybacking-enabled? b?:bool}:void
    set self._piggybacking-enabled? = b?
  }

  {getter protected {realtime?}:bool
    {return self.polling-enabled?}
  }

  {setter protected {connected? val:bool}:void
    {if self.connected? != val then
        {if val then
            {for cs in self.channel-sets do
                {for ma in cs.message-agents do
                    {type-switch ma
                     case c:AbstractConsumer do
                        {if c.subscribed? then
                            {self.enable-polling}
                        }
                    }
                }
            }
        }
        set super.connected? = val
    }
  }

  {method public {alarm-required?}:bool
    {return true}
  }

  {method public {alarm-running?}:bool
    {return self._alarm != null and self._alarm.scheduled?}
  }

  {method public {enable-polling}:void
    {inc self.polling-ref}
    {if self.polling-ref == 0 then
        {self.start-polling}
    }
  }

  {method public {disable-polling}:void
    {dec self.polling-ref}
    {if self.polling-ref < 0 then
        {self.stop-polling}
    }
  }

  {method public {poll}:void
    {self.internal-poll}
  }

  {method public {poll-failed rejected?:bool = false}:void
    {self.internal-disconnect rejected? = rejected?}
  }

  {method protected {start-polling}:void
    {if self._polling-enabled? then
        set self.should-poll? = true
        {self.poll}
    }
  }

  {method protected {stop-polling}:void
    {if-non-null alarm = self._alarm then
        {alarm.cancel}
    }
    set self.polling-ref = -1
    set self.should-poll? = false
    set self.poll-outstanding? = false
  }

  {method protected {internal-connect}:void
    || This implementation intentionally left blank.
  }

  {method protected {internal-disconnect rejected?:bool = false}:void
    {self.stop-polling}
    {super.internal-disconnect rejected? = rejected?}
  }

  {method protected {internal-poll event:#Event = null}:void
    {if not self.poll-outstanding? then
        {if {self.alarm-running?} then
            {self._alarm.cancel}
        }
        def command-message = {CommandMessage}
        set command-message.operation = CommandMessage.POLL_OPERATION
        {try
            {self.internal-send
                {new PollCommandMessageResponder, null, command-message, self}
            }
            set self.poll-outstanding? = true
         catch e:Exception do
            {self.stop-polling}
            {throw e}
        }
    }
  }

  {method public {send agent:MessageAgent, message:IMessage}:void
    let piggy-back?:bool = false
    {type-switch message
     case cm:CommandMessage do
        || Nothing
     else
        {if not self.poll-outstanding? and self._piggybacking-enabled? then
            {if self.should-poll? then
                set piggy-back? = true
             else
                def consumer-dispatcher =
                    {ConsumerMessageDispatcher.get-instance}
                {if {consumer-dispatcher.is-channel-used-for-subscriptions?
                        self}
                 then
                    set piggy-back? = true
                }
            }
        }
    }
    {if piggy-back? then
        {self.internal-poll}
    }

    {super.send agent, message}

    {if piggy-back? then
        def msg = {CommandMessage}
        set msg.operation = CommandMessage.POLL_OPERATION
        {try
            {self.internal-send
                {new PollCommandMessageResponder, null, msg, self}
            }
         catch e:Exception do
            {self.stop-polling}
            {throw e}
        }
    }
  }

  {method protected {connect-failed event:ChannelFaultEvent}:void
    {self.stop-polling}
    {super.connect-failed event}
  }

  {method protected {enable-alarm}:void
    {if-non-null alarm = self._alarm then
        {alarm.cancel}
        {alarm.schedule delay = self._polling-interval}
    }
  }

  {method protected {internal-send message-responder:MessageResponder}:void
    || This method intentionally left blank. Subclasses need to override this
    || method for any specific behavior.
  }

  {method final protected {get-message-responder
                        agent:MessageAgent,
                        message:IMessage
                    }:MessageResponder
    let responder:#MessageResponder = null
    {type-switch message
     case cm:CommandMessage do
        {switch cm.operation
         case CommandMessage.POLL_OPERATION do
            set responder = {new PollCommandMessageResponder, agent, cm, self}
        }
    }
    {if-non-null responder then
        {return responder}
     else
        {return {self.get-default-message-responder agent, message}}
    }
  }

  {method protected {get-default-message-responder
                        agent:MessageAgent,
                        message:IMessage
                    }:MessageResponder
    {return {super.get-message-responder agent, message}}
  }
}
