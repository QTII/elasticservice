||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{curl 8.0 applet}
{applet manifest = "../manifest.mcurl",
    {compiler-directives careful? = true}}

{import * from COM.CURL.CDK.DATA-OBJECT}
  
{value
    || metadata
    || --------------------
    def product-traits =
        {DataObjectTraits "Product",
            dynamic? = false,
            {StringArray 
                "productId",
                "name",
                "category",
                "price"}}
    || data object
    || --------------------
    def data =
        {DataObject traits = product-traits,
            productId = 1,
            name = "Nokia 3100 Blue",
            category = "9000",
            price = 109.0}
    || coordination
    || --------------------
    def dbc = {DataObjectDataBindingContext data}
    def update =
        {on ValueFinished at c:ValueControl do
            {c.update-data-binding "value"}}
    || presentation
    || --------------------
    def display =
        {Table cell-margin = 1px, margin = 1mm,
            {column-prototype halign = "right",
                {text ID},
                {text Name},
                {text Category},
                {text Price}},
            {column-prototype width = 1.5in,
                {TextDisplay halign = "right",
                    {bind value to "productId",
                        {format x:int as {String x}}}},
                {TextField update,
                    {bind value to "name"}},
                {TextField update,
                    {bind value to "category"}},
                {TextField halign = "right", update,
                    {bind value to "price",
                        {parse x:String as
                            {x.to-double}},
                        {format x:double as
                            {format "%5.2f", x}}}}}
        }
    || layout
    || --------------------
    {Dialog use-look-and-feel? = false,
        background = "#DDD",
        halign = "center",
        {VBox data-binding-context = dbc,
            spacing = 1mm,
            display,
            {HBox spacing = 1mm, margin = 1mm,
                {Fill},
                {CommandButton label = "Commit",
                    {on Action do
                        def report = {VBox}
                        {for k in data.traits.members do
                            {report.add
                                {paragraph {value k}: {value data[k]}}}}                    
                        {popup-message report}
                        {data.commit}
                    }},
                {CommandButton label = "Revert",
                    {on Action do
                        {data.revert}
                    }},
                {Fill}}
        }}
}
