||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{import * from CURL.LANGUAGE.REFLECTION}

{doc-next ||=== RecordSet-for
    {purpose The {ctext RecordSet-for} adapter implements the
        {docref RecordSet} protocol for data accessible 
        by get/set properties of Curl classes.}
}
{define-class public {RecordSet-for T:ClassType}
  {inherits GenericRecordSet}
  {doc-next ||=== RecordSet-for.subject-type
    {purpose The {docref ClassType} of underlying data instances.}
  }
  {getter public {subject-type}:ClassType
    {return T}
  }
  {doc-next ||=== RecordSet-for.create-subject
    {purpose Create a data instance.}
    {details Used by {docref RecordSet.new-record} when necessary.}
  }
  field public-get constant create-subject:{proc-type {}:T}
  {doc-next ||=== RecordSet-for.default
    {purpose Create a {ctext RecordSet-for} instance.}
    {parameter fields, the {docref RecordFields} characterizing
        the {docref-abbr RecordSet-for.subject-type}.}
    {parameter create-subject, function to create a data instance.
        If unspecified, this tries to use a no-arg constructor named 'default'.}
    {parameter ..., the rest arguments can be 
        data instances (of {docref-abbr RecordSet-for.subject-type})
        or {docref EventHandler}s.}
  }
  {constructor public {default 
                          fields:RecordFields = {fields-for T},
                          create-subject:#{proc-type {}:T} = null,
                          ...}
    set self.create-subject =
        {if-non-null create-subject then
            create-subject
         else
            {if-non-null create-subject =
                {if-non-null c = {(T asa ClassType).get-constructor "default"} ||""
                 then
                    {if (c.type asa ProcType).num-args == 0 then
                        {proc {}:T
                            {return {c.new} asa T}}
                     else null}
                 else null}
             then create-subject
             else
                {proc {}:T
                    || NOTE: this gives a mysterious failure
                    ||{return {uninitialized-value-for-type T}
                    {error
                        {lmessage 'RecordSet-for.new-record' can not create a '{value T}'
                            because '{value T}' does not have a no-arg 'default' constructor
                            and no 'create-subject' procedure was supplied.}}}
            }}
    {construct-super fields, {splice ...}}
  }
  {method public {new-record}:{RecordFacade-for T}
    def subject = {self.create-subject}
    {return {{RecordFacade-for T} subject, record-set = self}}
  }
  {method public {append val:any}:void
    {type-switch val
     case t:T do 
        let r:GenericRecord =
            {{RecordFacade-for T} t, record-set = self}
        {r.internal-attach self}
     case r:{RecordFacade-for T} do 
        {r.internal-attach self}
     else 
        {with-compiler-directives allow-any-calls? = true do
            {error {lmessage Type {value T} required for {value self}.append}}
        }}
  }
}
{doc-next ||=== RecordFacade
    {purpose Adapter implementing {docref Record} protocol for some subject.}
}
{define-class public abstract RecordFacade {inherits GenericRecord}
  {constructor {default record-set:#GenericRecordSet}
    {construct-super record-set}
  }
  {doc-next ||=== RecordFacade.subject-as-object
    {purpose The underlying {ctext subject}, as an {docref Object}.}
    {details This gives access to the subject in contexts where
        the specific type parameter is not known.}
  }
  {getter public abstract {subject-as-object}:Object}
}
{doc-next ||=== RecordFacade-for
    {purpose Adapter implementing {docref Record} protocol for some subject.}
}
{define-class public {RecordFacade-for T:ClassType}
  {inherits RecordFacade}
  {doc-next ||=== RecordFacade-for.subject
    {purpose The underlying data instance.}
  }
  field public-get constant subject:T
  || NOTE: docstring inherits
  {getter public {subject-as-object}:Object
    {return self.subject}
  }
  {doc-next ||=== RecordFacade-for.default
    {purpose Create a {ctext RecordFacade-for} instance.}
    {parameter subject, the underlying data instance.}
    {parameter record-set, the container {docref RecordSet-for}}
  }
  {constructor public {default 
                          subject:T,
                          record-set:#{RecordSet-for T} = null}
    set self.subject = subject
    {construct-super record-set}
  }
  {doc-next ||=== RecordFacade-for.from-subject
    {purpose Create a {ctext RecordFacade-for} instance.}
    {parameter subject, the underlying data instance.}
  }
  {factory public implicit {from-subject subject:T}:{RecordFacade-for T}
    {return {{RecordFacade-for T} subject}}
  }
  || implementation
  {method final inline {field-for k:String}:{RecordField-for T}
    {return {self.record-set.fields.get k} asa {RecordField-for T}}
  }
  {method protected {internal-get k:String}:any
    def f = {self.field-for k}
    {return {f.get-value self.subject}}
  }
  {method protected {internal-set k:String, x:any}:void
    def f = {self.field-for k}
    {f.set-value self.subject, x}
  }
  {method public {compare-field k:String, y:any}:int
    def f = {self.field-for k}
    def x = {f.get-value self.subject}
    {return
        {if-non-null x then
            {if-non-null y then
                {f.domain.compare x, y}
             else -1}
         else
            {if-non-null y then 1
             else 0}}}
  }
}
{doc-next ||=== fields-for
    {purpose Create {docref RecordFields} corresponding to
        the public properties of some {docref ClassType}.}
    {details Used by {docref RecordSet-for} as default value for
        {docref-abbr RecordSet-for.fields}.}
}
|| TODO: 'exclude' keyword arg
{define-proc public {fields-for t:ClassType,
                        sort?:bool = false
                    }:RecordFields
    def ms = 
        {t.get-members search-superclasses? = true,
            filter = ClassMember.get-property-filter}
    def fs = {{Array-of RecordField}}
    def seen = {{Set-of String}}
    {for m in ms do
        {type-switch m
         case p:Property do
            def name = p.name
            {if not {seen.member? name} then
                {seen.insert name}
                def settable? = {t.get-property-for-set name} != null
                def RF = {RecordField-for.construct t}
                def f =
                    {with-compiler-directives allow-any-calls? = true do
                        {RF p.name, 
                            domain = {domain-for p.type},
                            nullable? = p.type.can-be-null?, 
                            modifiable? = settable?} asa RecordField}
                {fs.append f}}}}
    {if sort? then
        {fs.sort 
            comparison-proc =
                {proc {a:RecordField, b:RecordField}:bool
                    {return {String-leq? a.name, b.name}}}}}
    def fields = {RecordFields {splice fs}}
    {return fields}
}
{define-class public {RecordField-for C:ClassType}
  {inherits DefaultRecordField}
  field public-get get-value:{proc-type {C}:any}
  field public-get set-value:{proc-type {C, any}:void}
  {constructor public {default name:String, ...}
    {construct-super name, {splice ...}}
    set self.get-value =
        {evaluate
            {expand-template 
                {proc {x:?{Literal C}}:any
                    {return x.?{Identifier self.name}}}
        }} asa {proc-type {C}:any}
    set self.set-value =
        {evaluate
            {expand-template 
                {proc {x:?{Literal C}, v:any}:void
                    {with-compiler-directives allow-implicit-any-casts? = true do
                        set x.?{Identifier self.name} = v}}
            }} asa {proc-type {C, any}:void}
  }
}
{define-proc {domain-for t:Type}:Domain
    let d:Domain = {Domain.from-type any}
    let constant domain-of:ParameterizedTypeTemplate = Domain-of
    {with-compiler-directives 
        allow-any-calls? = true,
        allow-implicit-any-casts? = true
     do
        || NOTE: there are no standard nullable domains
        || - but there's no need, because the RecordField can be nullable?
        let tt:Type =
            {if t.can-be-null? then
                {if-non-null nnv = t.non-null-variant
                 then nnv
                 else t}
             else t}
        {try
            set d = 
                {switch tt
                 case DateTime do
                    {StandardDateTimeDomain time-zone = DateTimeZone.utc}
                 else
                    {Domain.from-type tt}}
         catch e:Error do
            set d = {{domain-of.construct tt}}}}
    {return d}
}
{doc-next ||=== Domain-of
    {purpose A default {docref Domain} for an arbitrary {docref Type}.}
    {details Used by {docref fields-for}.}
}
|| TODO: need way to specialize these
|| -- registry of per-type defaults
{define-class public {Domain-of T:Type}
  {inherits {StandardDomain-of T}}
  {constructor public {default 
                          default-value:#T = {uninitialized-value-for-type T}, 
                          allowable-values:#{Array-of T} = null}
    {construct-super 
        || work around insistence on default-value
        {if {keyword-supplied? default-value} 
         then default-value
         else default-value-proc}, 
        null, null, allowable-values}
  }
  {method public {parse s:String}:any
    || in the hope there could be an implicit 'from-String'
    {return s}
  }
  {method public {compare x:any, y:any}:int
    || NOTE: comparing print representation => SLOW
    {return {{self.format x}.compare {self.format y}}}
  }
}
|| work around insistence on default-value
{define-proc {default-value-proc 
                 rf:RecordField, rs:RecordSet}:any
    {error {lmessage Need default-value}}
}
