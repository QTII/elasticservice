{curl-file-attributes character-encoding = "utf8"}
||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

{doc-next
    {purpose
        The object that handles communicating with a server or set of servers for
        a {docref RemoteObject}, {docref Consumer}, or {docref Producer}.
    }
}
{define-class public ChannelSet {inherits EventTarget}
  field private _authenticated?:bool = false
  field private _connected?:bool = false
  field private connecting?:bool = false
  field private _clustered?:bool
  field private _initial-destination-id:String = ""
  field private _channel-failover-uris:{HashTable-of String, {Array-of Url}}
  field private has-requested-cluster-endpoints?:bool = false
  field private _configured?:bool = false
  field private should-be-connected?:bool = false
  field private should-hunt?:bool = true
  field private hunting?:bool = false
  field private _channels:{Array-of Channel}
||--  field private _channel-ids:#{Array-of #String}
  field private _current-channel:#Channel
  field private current-channel-index:int = -1
  field private constant _message-agents:{Array-of MessageAgent}
  field private credentials:#String
  field private credentials-character-encoding:#CharEncoding
  field private auth-agent:#AuthenticationAgent
  field private constant pending-messages:{Set-of IMessage}
  field private constant pending-sends:{Array-of PendingSend}
  field private constant channel-event-handler:EventHandler

  {doc-next
    {purpose
        Construct a {docref ChannelSet} with one or more {docref Channel}s.
    }
    {parameter channel,
        A {docref Channel} to initialize this {docref ChannelSet} with.
        Only one of {param channel} and {docref channels} may be supplied.
    }
    {parameter channels,
        An array of {docref Channel}s to initialize this {docref ChannelSet} with.
        Only one of {param channel} and {docref channels} may be supplied.
    }
  }
  {constructor public {default
||--                          channel-ids:#{Array-of #String} = null,
                          clustered-with-load-balancing?:bool = false,
                          channel:#Channel = null,
                          channels:#{Array-of Channel} = null
                      }
    {construct-super}
    {if channel != null and channels != null then
        {error {lmessage Only one of 'channel' or 'channels' may be supplied.}}
    }
    set self.channel-event-handler =
        {on e:MessageEvent do
            {self.message-handler e}
        }
    set self._channels = {new {Array-of Channel}}
    {if-non-null channels then
        {for c in channels do
            {self.add-channel c}
        }
     else
        {if-non-null channel then
            {self.add-channel channel}
        }
    }

    set self._configured? = not self.channels.empty?
    set self.clustered? = clustered-with-load-balancing?
    set self._message-agents = {new {Array-of MessageAgent}}
    set self.pending-messages = {new {Set-of IMessage}}
    set self.pending-sends = {new {Array-of PendingSend}}
    set self._channel-failover-uris = {new {HashTable-of String, {Array-of Url}}}

    {self.add-event-handler
        {on e:ChannelConnectEvent do
            {for agent in self._message-agents do
                {agent.channel-connect-handler e}
            }
        }
    }
    {self.add-event-handler
        {on e:ChannelDisconnectEvent do
            {for agent in self._message-agents do
                {agent.channel-disconnect-handler e}
            }
        }
    }
    {self.add-event-handler
        {on e:ChannelFaultEvent do
            {for agent in self._message-agents do
                {agent.channel-fault-handler e}
            }
        }
    }
  }

  {getter protected {configured?}:bool
    {return self._configured?}
  }

  {getter public {connected?}:bool
    {return self._connected?}
  }

  {setter protected {connected? val:bool}:void
    {if self._connected? != val then
        def event = {PropertyChangeEvent.create-update-event
                        self,
                        "connected?",
                        self._connected?,
                        val
                    }
        set self._connected? = val
        {self.enqueue-event event}
        {self.set-authenticated?
            (val and self.current-channel != null and
             self.current-channel.authenticated?),
            credentials = self.credentials,
            notify-agents? = false
        }
    }
  }

||--  {getter public {channel-ids}:#{Array-of #String}
||--    {if-non-null self._channel-ids then
||--        {return self._channel-ids}
||--     else
||--        def ids = {new {Array-of #String}}
||--        {for channel in self.channels do
||--            {if-non-null channel then
||--                {ids.append channel.id}
||--             else
||--                {ids.append null}
||--            }
||--        }
||--        {return ids}
||--    }
||--  }

  {getter public {channels}:{Array-of Channel}
    {return self._channels}
  }

  {setter public {channels chs:{Array-of Channel}}:void
    {if self.configured? then
        {error {lmessage Cannot add when cofigured.}}
    }

    def chs-to-remove = {self._channels.clone}
    {for rc in chs-to-remove do
        {self.remove-channel rc}
    }
    {if not chs.empty? then
        {for ac in chs do
            {self.add-channel ac}
        }
    }
  }

  {getter public {current-channel}:#Channel
    {return self._current-channel}
  }

  {getter public {clustered?}:bool
    {return self._clustered?}
  }

  {setter public {clustered? val:bool}:void
    {if self._clustered? != val then
        ||TODO: handle channel-ids
        set self._clustered? = val
    }
  }

  {getter public {initial-destination-id}:String
    {return self._initial-destination-id}
  }

  {setter public {initial-destination-id val:String}:void
    set self._initial-destination-id = val
  }

  {getter protected {channel-failover-uris}:{HashTable-of String, {Array-of Url}}
    {return self._channel-failover-uris}
  }

  {setter protected {channel-failover-uris
                        val:{HashTable-of String, {Array-of Url}}
                    }:void
    set self._channel-failover-uris = val
    {for ch:Channel in self.channels do
        {if-non-null id = ch.id then
            def (uris, found?) =
                {self.channel-failover-uris.get-if-exists id}
            set self.current-channel.failover-uris = uris
        }
    }
  }

  {getter public {message-agents}:{Array-of MessageAgent}
    {return self._message-agents}
  }

  {getter public {authenticated?}:bool
    {return self._authenticated?}
  }

  {method protected {set-authenticated?
                     b?:bool,
                     credentials:#String = null,
                     notify-agents?:bool = true
                 }:void
    {if self._authenticated? != b? then
        def event = {PropertyChangeEvent.create-update-event
                        self,
                        "authenticated?",
                        self._authenticated?,
                        b?
                    }
        
        set self._authenticated? = b?
        {if not b? and
            self.auth-agent != null and
            self.auth-agent.state != AuthenticationAgent.LOGGED_OUT_STATE
         then
            {self.apply-logged-out-state {non-null self.auth-agent}}
        }
        {if notify-agents? then
            {for agent in self.message-agents do
                {agent.set-authenticated? b?, credentials = credentials}
            }
        }

        {self.enqueue-event event}
    }
  }

  {method public {connect agent:#MessageAgent}:void
    {if-non-null agent then
        {if {self._message-agents.find agent} == -1 then
            set self.should-be-connected? = true
            {self._message-agents.append agent}
            {agent.internal-set-channel-set self}
            {if self.connected? then
                {agent.channel-connect-handler
                    {ChannelConnectEvent
                        channel = self.current-channel,
                        reconnecting? = false,
                        rejected? = false,
                        connected? = self.connected?
                    }
                }
            }
        }
    }
  }

  {method public {disconnect agent:#MessageAgent}:void
    || FIXME: Should this send a synthetic disconnect event?
    {if-non-null agent then
        def agent-index = {self._message-agents.find agent}
        {if agent-index >= 0 then
            {self._message-agents.remove agent-index}
            {self.disconnect-agent-helper agent}
        }
        {if self._message-agents.size == 0 then
            set  self.should-be-connected? = false
            set self.current-channel-index = -1
            {if self.connected? then
                {self.disconnect-channel}
            }
        }
        {if agent.channel-set-mode == MessageAgent.AUTO_CONFIGURED_CHANNELSET then
            {agent.internal-set-channel-set null}
        }
     else
        {self.disconnect-all}
    }
  }
  {method private {disconnect-agent-helper agent:MessageAgent}:void
    || This will recurse, but this time the agent won't be found.
    || Should we really do both of these steps?
    {if self.connected? or self.connecting? then
        {agent.channel-disconnect-handler
            {ChannelDisconnectEvent
                channel = self._current-channel,
                connected? = self.connected?
            }
        }
     else
        {for i = self.pending-sends.size - 1 downto 0 do
            def ps = self.pending-sends[i]
            {if ps.agent == agent then
                {self.pending-sends.remove i}
                {self.pending-messages.remove
                    ps.message, error-if-missing? = false
                }
            }
        }
    }
  }

  {method public {disconnect-all}:void
    || FIXME: Should this send synthetic disconnect events?
    def agents = {self._message-agents.clone}
    {self._message-agents.clear}
    {for agent in agents do
        || This will call disconnect, but the agent won't be found.
        {self.disconnect-agent-helper agent}
    }
  }

  {doc-next
    {purpose
        Provide server credentials to authenticate with a server.
    }
    {parameter username,
        Username for credentials.
    }
    {parameter password,
        Password for credentials.
    }
    {parameter character-encoding,
        {docref CharEncoding} to encode credentials default is {ctext null} which means
        {ctext CharEncoding.iso-latin-1}.
        The only other supported {docref CharEncoding} is {ctext CharEncoding.utf8}.
    }
  }
  {method public {login
                     username:#String,
                     password:#String,
                     character-encoding:#CharEncoding = null,
                     agent:#MessageAgent = null
                 }:#AuthenticationAgent

    || FIXME: why not allow any character-encoding?
    || Use legacy charset if not utf8
    {if character-encoding != CharEncoding.utf8 then
        set character-encoding = null
    }
    def creds = {encode-credentials username, password, character-encoding}

    {if-non-null agent then
        {agent.set-credentials creds, character-encoding = character-encoding}
        {return null}
     else
        {return
            {self.internal-login creds, character-encoding = character-encoding}
        }
    }
  }
  
  {method private {internal-login
                      credentials:#String,
                      character-encoding:#CharEncoding = null
                  }:AuthenticationAgent
    {if self.authenticated? then
        {error {lmessage Channel Set is already authenticated.}}
    }
    
    {if self.auth-agent != null and
        self.auth-agent.state != AuthenticationAgent.LOGGED_OUT_STATE
     then
        {error {lmessage ChannelSet is in the process of logging in or logging out.}}
    }

    || Create CommandMessage for logging in.
    def cmd = {self.create-auth-message
                  CommandMessage.LOGIN_OPERATION,
                  credentials = {if-non-null credentials then
                                    credentials
                                 else
                                    ""
                                },
                  character-encoding = character-encoding
              }
    
    {if self.auth-agent == null then
        set self.auth-agent = {AuthenticationAgent self}}
    set self.auth-agent.state = AuthenticationAgent.LOGGING_IN_STATE
    {self.send {non-null self.auth-agent}, cmd}
    {return {non-null self.auth-agent}}
  }
  
  {doc-next
    {purpose
        Logout from the server.
    }
  }
  {method public {logout
                     agent:#MessageAgent = null,
                     force?:bool = false
                 }:#AuthenticationAgent
    {if (not force?) and
        (agent != null and self.auth-agent == null) then
        set self.credentials = null
        {for ch in self.channels do
            {ch.logout agent = agent}
        }
        {return null}
     else
        {if self.auth-agent != null and
            (self.auth-agent.state == AuthenticationAgent.LOGGING_IN_STATE or
             self.auth-agent.state == AuthenticationAgent.LOGGING_OUT_STATE)
         then
            {error {lmessage ChannelSet is in the process of logging in or logging out.}}
        }

        {self.sync-credentials null}
        def cmd = {self.create-auth-message
                      CommandMessage.LOGOUT_OPERATION}

        {if self.auth-agent == null then
            set self.auth-agent = {AuthenticationAgent self}}
        set self.auth-agent.state = AuthenticationAgent.LOGGING_OUT_STATE
        {self.send {non-null self.auth-agent}, cmd}
        {return self.auth-agent}
    }
  }

  {method protected {authenticate-succeeded
                        agent:AuthenticationAgent,
                        cmd:CommandMessage,
                        ack:AcknowledgeMessage
                    }:void
    {agent.call-result ack}

    def login? = (cmd.operation == CommandMessage.LOGIN_OPERATION)
    {if login? then
        || when LOGIN is successful.
        {self.apply-logged-in-state agent, cmd}
     else
        || when LOGOUT is successful.
        {self.apply-logged-out-state agent}
    }
    
||--    {agent.call-result ack}
    {self.enqueue-event
        {if login? then
            {AuthenticationLoggedInEvent message = ack}
         else
            {AuthenticationLoggedOutEvent message = ack}
        }
    }
  }

  {method private {apply-logged-in-state
                      agent:AuthenticationAgent,
                      cmd:CommandMessage
                  }:void
    {self.sync-credentials (cmd.body asa String)}
    set agent.state = AuthenticationAgent.LOGGED_IN_STATE
    set self.current-channel.authenticated? = true
  }

  {method private {apply-logged-out-state
                      agent:AuthenticationAgent
                  }:void
    set agent.state = AuthenticationAgent.SHUTDOWN_STATE
    set self.auth-agent = null
    {self.disconnect agent}
    set self.current-channel.authenticated? = false
  }

  {method protected {authenticate-failed
                        agent:AuthenticationAgent,
                        cmd:CommandMessage,
                        err:ErrorMessage
                    }:void
    set self.auth-agent = null
    {self.disconnect agent}
    {agent.call-status err}

    set agent.state = AuthenticationAgent.SHUTDOWN_STATE
    {self.enqueue-event
        {AuthenticationFaultEvent message = err}
    }
  }

  {method private {create-auth-message
                      operation:uint,
                      credentials:#String = null,
                      character-encoding:#CharEncoding = null
                  }:CommandMessage
    def cmd = {CommandMessage}
    set cmd.operation = operation
    set cmd.body = credentials
    set cmd.destination = "auth" ||""
    {if-non-null character-encoding then
        set cmd.headers[CommandMessage.CREDENTIALS_CHARSET_HEADER] =
            {char-encoding-to-http-charset character-encoding}
    }
    {return cmd}
  }

  {method private {sync-credentials val:#String}:void
    set self.credentials = val

    || Set credentials to all MessageAgents
    {for ma in self.message-agents do
        set ma.credentials = val
    }

    || Set credentials to all Channels
    {for ch in self.channels do
        set ch.credentials = val
    }
  }
  
  {method public {send agent:MessageAgent, message:IMessage}:void
    {if self.current-channel != null and self.connected? then
        
        {type-switch message
         case cm:CommandMessage do
            {if cm.operation == CommandMessage.TRIGGER_CONNECT_OPERATION then
                def ack = {AcknowledgeMessage}
                set ack.client-id = agent.client-id
                set ack.correlation-id = message.message-id
                {agent.acknowledge ack, message}
                {return}
            }
        }

        {if not self.has-requested-cluster-endpoints? and self.clustered? then
            def cmd = {CommandMessage}
            {if agent isa AuthenticationAgent then
                set cmd.destination = self.initial-destination-id
             else
                set cmd.destination = agent.destination
            }
            set cmd.operation = CommandMessage.CLUSTER_REQUEST_OPERATION
            {self.current-channel.send-cluster-request
                {ClusterMessageResponder cmd, self}}
            set self.has-requested-cluster-endpoints? = true
        }

        {self.current-channel.send agent, message}
     else
        {if not {self.pending-messages.member? message} then
            {self.pending-messages.insert message}
            {self.pending-sends.append {new PendingSend, agent, message}}
        }
        {if not self.connecting? then
            {if self.current-channel == null or
                self.current-channel-index == -1
             then
                {self.hunt}
            }
            {type-switch self.current-channel
             case ccc:CurlConnectionChannel do
||--                || TODO: Flex has a special case for NetConnectionChannels that
||--                ||       accounts for some reconnection idyosyncrasies. For now,
||--                ||       we're just connecting the channel.
||--                {self.connect-channel}
                {after 0s do
                    {self.connect-channel}
                }
             case n:Null do
                {return}
             else
                {self.connect-channel}
            }
        }
    }
  }

  {method private {hunt}:bool
    {if self.channels.empty? then
        {error {lmessage No channels are available for use.}}
    }
    
    {if-non-null self.current-channel then
        {self.disconnect-channel}
    }
    
    {inc self.current-channel-index}
    {if self.current-channel-index >= self.channels.size or
        self.current-channel-index < 0
     then
        set self.current-channel-index = -1
        {return false}
    }
    {if self.current-channel-index > 0 then
        set self.hunting? = true
    }
    {if self.configured? then
||--        {if-non-null channel = self.channels[self.current-channel-index] then
        def channel = self.channels[self.current-channel-index]
        set self._current-channel = channel
||--         else
            || TODO: This implementation iterates over the channels, but the
            ||       Flex implementation does it differently. Figure out what's
            ||       different.
||--            {if-non-null channel-ids = self.channel-ids then
||--                {for id in channel-ids do
||--                    {if-non-null id then
||--                        set self.current-channel = {ServerConfig.get-channel id}
||--                        || TODO: Apply other channel properties found in the
||--                        ||       channel configuration (services-config.xml),
||--                        ||       such as <polling-enabled>.
||--                        {if-non-null self.current-channel then
||--                            {break}
||--                        }
||--                    }
||--                }
||--                {if-non-null c = self.current-channel then
||--                    {c.set-credentials self.credentials}
||--                    set self.channels[self.current-channel-index] = c
||--                 else
||--                    {throw {new Exception, "NoChannelAvailableError"}}
||--                }
||--             else
||--                    {error {lmessage No channel available.}}
||--            }
||--        }
     else
        set self._current-channel = self.channels[self.current-channel-index]
    }

    {if-non-null current-channel = self.current-channel then
        {if-non-null id = current-channel.id then
            def (failover-uris, found?) =
                {self.channel-failover-uris.get-if-exists id}
            {if found? then
                set current-channel.failover-uris = failover-uris
            }
        }
    }
    {return true}
  }

  {method public {set-credentials
                     credentials:#String,
                     agent:#MessageAgent = null,
                     character-encoding:#CharEncoding = null
                 }:void
    set self.credentials = credentials
    {for channel in self.channels do
        {channel.set-credentials
            self.credentials,
            agent = agent,
            character-encoding = character-encoding
        }
    }
  }

  {method public {add-channel channel:Channel}:void
    {if self.configured? then
        {error
            {lmessage Channels cannot be added to a ChannelSet that targets a configured destination.}
        }
    }
    {if self.clustered? and channel.id == null then
        {error {lmessage Cannot add null id channel when clustered.}}
    }

    {if {self.channels.find channel} != -1 then
        {return}
    }
    {self.channels.append channel}
    {if-non-null self.credentials then
        {channel.set-credentials
            self.credentials,
            agent = null,
            character-encoding = self.credentials-character-encoding
        }
    }
  }

  {method public {remove-channel channel:Channel}:void
    {if self.configured? then
        {error {lmessage Cannot remove channels when configured.}}
    }
    def channel-index = {self.channels.find channel}
    {if channel-index >= 0 then
        {self.channels.remove channel-index}
        {if channel == self.current-channel then
            {if self.connected? then
                set self.should-hunt? = false
                {self.disconnect-channel}
            }
            set self._current-channel = null
            set self.current-channel-index = -1
        }
    }
  }

  {method protected {connect-channel}:void
    {if not self.connected? and not self.connecting? then
        set self.connecting? = true
        {self.current-channel.connect self}
        || Listen for any server pushed messages on the Channel.
        {if not {self.current-channel.event-handler-present? self.channel-event-handler} then
            {self.current-channel.add-event-handler self.channel-event-handler}
        }
    }
  }
  {method protected {disconnect-channel}:void
    set self.connecting? = false
    || Stop listening for server pushed messages on the Channel.
    {if {self.current-channel.event-handler-present? self.channel-event-handler} then
        {self.current-channel.remove-event-handler self.channel-event-handler}
    }
    {self.current-channel.disconnect self}
  }

  {method protected {message-handler event:MessageEvent}:void
    {self.enqueue-event event}
  }

  || Called by the current Channel.
  {method protected {channel-connect-handler e:ChannelConnectEvent}:void
    set self.connecting? = false
    set self.connected? = true
    set self.current-channel-index = -1
    
    {while not self.pending-sends.empty? do
        def ps = self.pending-sends[0]
        {self.pending-sends.remove 0}
        def ps-msg = ps.message
        {self.pending-messages.remove ps-msg, error-if-missing? = false}
        {type-switch ps-msg
         case cm:CommandMessage do
            {if cm.operation == CommandMessage.TRIGGER_CONNECT_OPERATION then
                def ack = {AcknowledgeMessage}
                set ack.client-id = ps.agent.client-id
                set ack.correlation-id = cm.message-id
                {ps.agent.acknowledge ack, cm}
                {continue}
            }
            {if not ps.agent.config-requested? and
                ps.agent.needs-config? and
                cm.operation == CommandMessage.CLIENT_PING_OPERATION
             then
                set cm.headers[CommandMessage.NEEDS_CONFIG_HEADER] = true
                set ps.agent.config-requested? = true
            }
        }
        {self.send ps.agent, ps-msg}
    }

    {if self.hunting? then
        set e.reconnecting? = true
        set self.hunting? = false
    }
    
    || TODO: Refire, but why?
    {self.enqueue-event e}
    def change-event = {PropertyChangeEvent.create-update-event
                           self,
                           "connected?",
                           false,
                           true
                       }
    {self.enqueue-event change-event}
  }

  || Called by the current Channel.
  {method protected {channel-disconnect-handler e:ChannelDisconnectEvent}:void
    set self.connecting? = false
    set self.connected? = false

    {if self.should-be-connected? and
        not e.reconnecting? and
        not e.rejected?
     then
        {if self.should-hunt? and {self.hunt} then
            set e.reconnecting? = true
            {self.enqueue-event e}
            {if self.current-channel isa CurlConnectionChannel then
                {after 0s do
                    {self.connect-channel}}
             else
                {self.connect-channel}
            }
         else
            {self.enqueue-event e}
            {self.fault-pending-sends e}
        }
     else
        {self.enqueue-event e}
        {if e.rejected? then
            {self.fault-pending-sends e}
        }
    }

    set self.should-hunt? = true
  }

  {method protected {channel-fault-handler e:ChannelFaultEvent}:void
    {if e.channel.connected? then
        {self.enqueue-event e}
     else
        set self.connecting? = false
        set self.connected? = false

        {if self.should-be-connected? and
            not e.reconnecting? and
            not e.rejected?
         then
            {if {self.hunt} then
                set e.reconnecting? = true
                {self.enqueue-event e}
                {if self.current-channel isa CurlConnectionChannel then
                    || Insert slight delay for CurlConnectionChannel
                    || to shut down and clean up in preparation for next connect.
                    {after 0s do
                        {self.connect-channel}}
                 else
                    {self.connect-channel}
                }
                
             else
                {self.enqueue-event e}
                {self.fault-pending-sends e}
            }

         else
            {self.enqueue-event e}
            {if e.rejected? then
                {self.fault-pending-sends e}}
        }
    }
  }

  {method protected {fault-pending-sends e:ChannelEvent}:void
    {while (not self.pending-sends.empty?) do
        def ps = self.pending-sends[0]
        def ps-msg = ps.message
        {self.pending-sends.remove 0}
        {self.pending-messages.remove ps-msg}

        def err-msg = {ErrorMessage}
        set err-msg.correlation-id = ps-msg.message-id
        set err-msg.headers[ErrorMessage.RETRYABLE_HINT_HEADER] = true
        set err-msg.fault-code = "Client.Error.MessageSend"
        set err-msg.fault-string = {lmessage messaging send failed}||

        {type-switch e
         case f-event:ChannelFaultEvent do
            set err-msg.fault-detail =
                f-event.fault-code & " " & f-event.fault-string & " " & f-event.fault-detail
            {if f-event.fault-code == "Channel.Authentication.Error" then
                set err-msg.fault-code = f-event.fault-code
            }
         else
            set err-msg.fault-detail =
                {lmessage messaging cannot connect to destination} ||
        }
        set err-msg.root-cause = e
        {ps.agent.fault err-msg, ps-msg}
    }
  }
}

{define-proc {encode-credentials
                 _username:#String,
                 _password:#String,
                 character-encoding:#CharEncoding
             }:#String
    {if _username == null and _password == null then
        {return null}
    }
    def username = {if-non-null val = _username then val else ""}
    def password = {if-non-null val = _password then val else ""}
    def credentials = username & ":" & password
    def encoding =
        {if-non-null character-encoding then
            {if character-encoding == CharEncoding.none-specified then
                CharEncoding.utf8
             else
                character-encoding
            }
         else
            CharEncoding.iso-latin-1 || default from Adobe
        }
    def bytes = {ByteArray}
    {with-open-streams out =
        {TranscodingTextOutputStream
            {ByteArrayOutputStream bytes}, encoding, false}
     do
        {out.write-one-string credentials}}
    {return
        {base64-encode bytes}}
}


{define-class ClusterMessageResponder {inherits MessageResponder}

  field private channel-set:ChannelSet

  {constructor {default
                   msg:IMessage,
                   channel-set:ChannelSet
               }
    {construct-super null, msg}
    set self.channel-set = channel-set
  }

  {method protected {init-handlers}:void
    set self.result-handler =
        {proc {msg:IMessage}:void
            {if-non-null body = msg.body then
                {type-switch body
                 case ea:Array do
                    def failover-uris = {{HashTable-of String, {Array-of Url}}}
                    {for e in ea do
                        def channel = e asa DataObject
                        {for e-uri key id in channel do
                            {if not {failover-uris.key-exists? id} then
                                set failover-uris[id] = {{Array-of Url}}
                            }
                            {failover-uris[id].append {url e-uri asa String}}
                        }
                    }
                    set self.channel-set.channel-failover-uris = failover-uris
                }
            }
        }
  }
}
